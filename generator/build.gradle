plugins {
    id "de.undercouch.download" version "1.2"
}

apply plugin: 'de.undercouch.download'

dependencies {
    compile project(':core')
    compile 'com.squareup:javapoet:1.12.1'
}

import de.undercouch.gradle.tasks.download.Download

task generateMessages(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    main = 'pt.lsts.imc4j.generator.IMCGenerator'
    workingDir = 'src/main/'
    // arguments to pass to the application

    args "$parent.rootDir/core/src/main/resources/IMC.xml", "$parent.rootDir/core/src/main/java"
}

task downloadImc(type: Download) {
    src 'https://raw.githubusercontent.com/LSTS/imc/master/IMC.xml'
    dest 'core/src/main/resources/IMC.xml'

    doLast {
        println("IMC.xml downloaded to '$dest'.")
        def commit = new groovy.json.JsonSlurper().parse('https://api.github.com/repos/LSTS/imc/commits/master'.toURL()).sha;
        File shaFile = new File('core/src/main/resources/IMC.sha')
        shaFile.text = "$commit";
        println("IMC SHA: ${commit}")
    }
}

task updateImc {
    description = 'Download IMC from master and generate classes'
    group = 'Generator'
    dependsOn = [downloadImc, generateMessages]
}